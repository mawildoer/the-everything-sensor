import BME280 from "bme280/elec/src/bme280.ato"
import LIS2DH12TR from "lis2dh12tr/elec/src/lis2dh12tr.ato"
import TMC2209 from "tmc2209/elec/src/tmc2209.ato"
import HX711 from "hx711/elec/src/hx711.ato"
import ATGM336H_5N31 from "atgm336h-5n31/elec/src/atgm336h-5n31.ato"
import SGP40_D_R4 from "sgp40-d-r4/elec/src/sgp40-d-r4.ato"
import MAX31856MUDT from "max31856mudt/elec/src/max31856mudt.ato"
import NAU7802 from "nau7802/elec/src/nau7802.ato"
import ESP32S3 from "esp32-s3/esp32-s3.ato"
import RP2040Kit from "rp2040/RP2040Kit.ato"
import USBCConn from "usb-connectors/usb-connectors.ato"
import TCA9548APWR from "tca9548apwr/elec/src/tca9548apwr.ato"
import LDK220M_R_3V3 from "ldk220m-r/elec/src/ldk220m-r.ato"
import SaleaeHeader from "saleae-header/saleae-header.ato"
import TF_01A from "tf-01a/elec/src/tf-01a.ato"
import XT30_Male_Right_Angle from "xt-connectors/xt-connectors.ato"

import LEDIndicatorGreen from "generics/leds.ato"
import LEDIndicatorRed from "generics/leds.ato"
import DiffPair from "generics/interfaces.ato"
import Power from "generics/interfaces.ato"
import I2C from "generics/interfaces.ato"
import SPI from "generics/interfaces.ato"
import Resistor from "generics/resistors.ato"

import XY308_2_54_4P from "XY308-2.54-4P.ato"
import DB125_2_54_2P_GN from "DB125-2.54-2P-GN.ato"
import DB128V_5_08_4P_GN_S from "DB128V-5.08-4P-GN-S.ato"
import ButtonSKRPACE010 from "generics/buttons.ato"

import JTAG_10PIN from "programming-headers/elec/src/programming-headers.ato"

module LoadCellTerminal:
    sense = new DiffPair
    force = new Power
    # connect amp to load cell terminal
    load_cell_terminal = new XY308_2_54_4P
    sense.p ~ load_cell_terminal.1
    sense.n ~ load_cell_terminal.2
    force.vcc ~ load_cell_terminal.3
    force.gnd ~ load_cell_terminal.4

module I2CSensors:
    power3v3 = new Power
    i2c = new I2C

    # I2C multiplexer
    i2c_mux = new TCA9548APWR
    power3v3 ~ i2c_mux.power
    i2c ~ i2c_mux.i2c

    # Pressure, temperature, humidity sensor
    bme280 = new BME280
    power3v3 ~ bme280.power
    i2c_mux.i2c0 ~ bme280.i2c

    # Accelerometer
    lis2dh12tr = new LIS2DH12TR
    power3v3 ~ lis2dh12tr.power
    i2c_mux.i2c1 ~ lis2dh12tr.i2c

    # Load cell amplifier
    nau7802 = new NAU7802
    power3v3 ~ nau7802.power
    i2c_mux.i2c2 ~ nau7802.i2c

    nau_terminal = new LoadCellTerminal
    nau7802.force ~ nau_terminal.force
    nau7802.sense ~ nau_terminal.sense

    # Air quality sensor
    sgp40_d_r4 = new SGP40_D_R4
    i2c_mux.i2c3 ~ sgp40_d_r4.i2c
    sgp40_d_r4.power ~ power3v3

module I2CSeriesResistors:
    i2c_in = new I2C
    i2c_out = new I2C
    # series resistors
    r1 = new Resistor
    r2 = new Resistor

    r1.value = 0ohm to 1ohm
    r2.value = 0ohm to 1ohm
    r1.package = "0402"
    r2.package = "0402"

    i2c_in.sda ~ r1.1; r1.2 ~ i2c_out.sda
    i2c_in.scl ~ r2.1; r2.2 ~ i2c_out.scl

module Micros:
    power3v3 = new Power
    i2c = new I2C

    # ESP32
    esp32 = new ESP32S3
    esp32.power ~ power3v3
    esp_i2c_res = new I2CSeriesResistors
    i2c ~ esp_i2c_res.i2c_in; esp_i2c_res.i2c_out ~ esp32.i2c

    # Jtag header
    jtag_header = new JTAG_10PIN
    jtag_header.jtag ~ esp32.jtag

    # RP2040
    rp2040 = new RP2040Kit
    rp2040.power ~ power3v3
    rp_i2c_res = new I2CSeriesResistors
    i2c ~ rp_i2c_res.i2c_in; rp_i2c_res.i2c_out ~ rp2040.i2c

module TheEverythingSensor:
    power3v3 = new Power
    power5v = new Power

    usb_c_esp32 = new USBCConn
    usb_c_rp2040 = new USBCConn
    sensors = new I2CSensors
    power = new Power
    micros = new Micros


    micros.rp2040.reset_btn.btn -> ButtonSKRPACE010
    micros.rp2040.boot_btn.btn -> ButtonSKRPACE010

    # Power
    power5v = new Power
    ldo3v3 = new LDK220M_R_3V3
    power3v3 = new Power

    # connect USBC to power input
    usb_c_esp32.power ~ power5v
    usb_c_rp2040.power ~ power5v

    # connect USBC data to microcontrollers
    usb_c_esp32.usb2 ~ micros.esp32.usb2
    usb_c_rp2040.usb2 ~ micros.rp2040.usb2

    # connect power to LDO
    power5v ~ ldo3v3.power_in
    ldo3v3.power_out ~ power3v3
    micros.power3v3 ~ power3v3

    # Power indicator
    power_indicator = new LEDIndicatorGreen
    power3v3 ~ power_indicator.power

    # connect sensors
    power3v3 ~ sensors.power3v3
    micros.i2c ~ sensors.i2c

    # Stepper motor driver
    tmc2209 = new TMC2209
    # xt30 input for power
    tmc2209.step_dir.dir ~ micros.rp2040.gpio0
    tmc2209.step_dir.step ~ micros.rp2040.gpio1

    tmc2209.enable.io ~ micros.rp2040.gpio2

    # saleae header - stepper motor + power rails
    saleae_header_stepper_motor = new SaleaeHeader
    saleae_header_stepper_motor.ch1.io ~ micros.rp2040.gpio0
    saleae_header_stepper_motor.ch2.io ~ micros.rp2040.gpio1
    saleae_header_stepper_motor.ch3.io ~ power3v3.vcc
    saleae_header_stepper_motor.ch4.io ~ power5v.vcc
    saleae_header_stepper_motor.gnd ~ power3v3.gnd

    # XT30 output for power
    xt30 = new XT30_Male_Right_Angle
    xt30.power ~ tmc2209.power
    #connect grounds together
    xt30.gnd ~ power3v3.gnd

    # 4 pin output to stepper motor
    stepper_motor = new DB128V_5_08_4P_GN_S
    tmc2209.motor_output.a1 ~ stepper_motor.1
    tmc2209.motor_output.a2 ~ stepper_motor.2
    tmc2209.motor_output.b1 ~ stepper_motor.3
    tmc2209.motor_output.b2 ~ stepper_motor.4

    # SD card
    sd_card = new TF_01A
    power3v3 ~ sd_card.power
    sd_card.spi ~ micros.rp2040.spi

    #saleae header - rp2040 spi
    saleae_header_rp2040_spi = new SaleaeHeader
    saleae_header_rp2040_spi.spi ~ micros.rp2040.spi

    # GPS
    atgm336h_5n31 = new ATGM336H_5N31
    power3v3 ~ atgm336h_5n31.power
    micros.rp2040.uart ~ atgm336h_5n31.uart

    # Saleae header - rp2040 uart + i2c
    saleae_header_rp2040_uart_i2c = new SaleaeHeader
    saleae_header_rp2040_uart_i2c.uart ~ micros.rp2040.uart
    micros.i2c ~ saleae_header_rp2040_uart_i2c.i2c

    # Thermocouple amplifier
    max31856mudt = new MAX31856MUDT
    max31856mudt.power ~ power3v3
    micros.esp32.spi2 ~ max31856mudt.spi

    # 2 pin thermocouple input
    thermocouple = new DB125_2_54_2P_GN
    max31856mudt.tc_input.p ~ thermocouple.1
    max31856mudt.tc_input.n ~ thermocouple.2

    # saleae header - esp32 spi
    saleae_header_esp32_spi = new SaleaeHeader
    saleae_header_esp32_spi.spi ~ micros.esp32.spi2

    # Load cell amplifier
    hx711 = new HX711
    power3v3 ~ hx711.power
    # just need clock and data
    micros.rp2040.gpio17 ~ hx711.clock
    micros.rp2040.gpio18 ~ hx711.data

    hx711_terminal = new LoadCellTerminal
    hx711.force ~ hx711_terminal.force
    hx711.sense_a ~ hx711_terminal.sense

